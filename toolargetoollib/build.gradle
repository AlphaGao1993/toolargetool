apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.0'
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

group = 'com.gu.android'
version = '0.1.1'

install {
    repositories.mavenInstaller {
        pom.project {
            name 'toolargetool'
            description ' A tool for debugging TransactionTooLargeException on Android '
            url 'https://github.com/guardian/toolargetool'
            inceptionYear '2017'

            packaging 'aar'
            groupId 'com.gu.android'
            artifactId 'toolargetool'
            version '0.1.1'

            licenses {
                license {
                    name 'The MIT License (MIT)'
                    url 'https://opensource.org/licenses/MIT'
                }
            }
            scm {
                connection 'https://github.com/guardian/toolargetool.git'
                url 'https://github.com/guardian/toolargetool'

            }
            developers {
                developer {
                    id = 'maxspencer'
                    name 'Max Spencer'
                }
            }
        }
    }
}

Properties bintrayProperties = new Properties()
try {
    bintrayProperties.load(project.file('bintray.properties').newDataInputStream())
} catch (FileNotFoundException e) {
    // ignored
}

bintray {
    user = bintrayProperties.getProperty('user')
    key = bintrayProperties.getProperty('key')
    configurations = ['archives']
    pkg {
        repo = 'android'
        name = 'toolargetool'
        userOrg = 'guardian'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/guardian/toolargetool.git'
        publish = true
        version {
            name = '0.1.1'
            desc = 'toolargetool version 0.1.1'
            released = new Date()
            vcsTag = 'v0.1.1'
        }
    }
}
